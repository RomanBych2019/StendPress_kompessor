#include         <SoftwareSerial.h>                   //  Подключаем библиотеку для работы по программной шине UART
#include         <Adafruit_Thermal.h>                 //  Подключаем библиотеку для работы с принтером
SoftwareSerial   mySerial(5, 6);                      //  Объявляем объект библиотеки SoftwareSerial, указывая задействованные выводы Arduino (RX=5-зелёный, TX=6-синий). Можно назначить другие выводы Arduino
Adafruit_Thermal printer(&mySerial);                  //  Объявляем объект библиотеки Adafruit_Thermal, указывая ссылку на созданный ранее объект mySerial библиотеки SoftwareSerial
                                                      //
void setup(){                                         //  Код функции setup выполняется только 1 раз, при старте скетча
    mySerial.begin(9600);                             //  Инициируем передачу данных по программной шине UART на скорости 9600. Функцию begin объекта mySerial нужно вызвать до вызова функции begin объекта printer!
    printer.begin();                                  //  Инициируем работу с термопринтером. В качестве параметра можно указать время нагрева пикселей от 3 (0,03 мс) до 255 (2,55 мс), чем выше тем темнее пикселы. Значение по умолчанию = 120 (1,20 мс)
                                                      //
    printer.setSize('L');                             //  Устанавливаем крупный размер шрифта 'L' (Large)
    printer.println("Large");                         //  Выводим текст
    printer.setSize('M');                             //  Устанавливаем средний размер шрифта 'M' (Medium)
    printer.println("Medium");                        //  Выводим текст
    printer.setSize('S');                             //  Устанавливаем маленький размер шрифта 'S' (Small) - используется по умолчанию
    printer.println("Small");                         //  Выводим текст
                                                      //
    printer.justify('R');                             //  Устанавливаем выравнивание текста по правому краю 'R' (Right)
    printer.println("Right");                         //  Выводим текст
    printer.justify('C');                             //  Устанавливаем выравнивание текста по центру 'C' (Center)
    printer.println("Center");                        //  Выводим текст
    printer.justify('L');                             //  Устанавливаем выравнивание текста по левому краю 'L' (Left) - используется по умолчанию
    printer.println("Left");                          //  Выводим текст
                                                      //
    printer.inverseOn();                              //  Устанавливаем инверсию цвета текста.
    printer.println("Inverse");                       //  Выводим текст
    printer.inverseOff();                             //  Отменяем инверсию цвета текста.
                                                      //
    printer.doubleHeightOn();                         //  Устанавливаем удвоенную высоту текста.
    printer.println("Double Height");                 //  Выводим текст
    printer.doubleHeightOff();                        //  Отменяем удвоенную высоту текста.
                                                      //
    printer.doubleWidthOn();                          //  Устанавливаем удвоенную ширину текста.
    printer.println("Double Width");                  //  Выводим текст
    printer.doubleWidthOff();                         //  Отменяем удвоенную ширину текста.
                                                      //
    printer.boldOn();                                 //  Устанавливаем полужирное начертание текста.
    printer.println("Bold");                          //  Выводим текст
    printer.boldOff();                                //  Отменяем полужирное начертание текста.
                                                      //
    printer.underlineOn();                            //  Устанавливаем подчёркивание текста линией в 1 пиксель. Равнозначно вызову функции с параметром 1: printer.underlineOn(1);
    printer.println("Underline 1px");                 //  Выводим текст
    printer.underlineOn(2);                           //  Устанавливаем подчёркивание текста линией в 2 пикселя.
    printer.println("Underline 2px");                 //  Выводим текст
    printer.underlineOff();                           //  Отменяем подчёркивание текста.
                                                      //
    printer.setLineHeight(25);                        //  Устанавливаем межстрочный интервал в 2,5 мм. В качестве параметра можно указать интервал от 24 (2,4 мм) до 32767. Значение по умолчанию = 30 (3,0 мм)
    printer.println("1\r\n2\r\n3");                   //  Выводим 3 строки текста
    printer.setLineHeight(60);                        //  Устанавливаем межстрочный интервал в 6,0 мм.
    printer.println("4\r\n5\r\n6");                   //  Выводим 3 строки текста
    printer.setLineHeight();                          //  Устанавливаем межстрочный в значение по умолчанию (3,0 мм)
                                                      //
    printer.setCharSpacing(10);                       //  Устанавливаем дополнительный межсимвольный интервал в 10 пикселов. В качестве параметра можно указать интервал от 0 до 32767 пикселей. Значение по умолчанию = 0
    printer.println("CharSpacing");                   //  Выводим текст
    printer.setCharSpacing();                         //  Отменяем дополнительный межсимвольный интервал. Равнозначно вызову функции с параметром 0: printer.setCharSpacing(0);
                                                      //
    printer.tab();                                    //  Устанавливаем отступ (табуляцию).
    printer.println("<- Tabulation");                 //  Выводим текст
                                                      //
    printer.feedRows(10);                             //  Прокручиваем кассовую ленту на 10 пикселей. В качестве параметра можно указать от 0 до 255 пикселей.
                                                      //
    if(printer.hasPaper()){                           //  Если кассовая лента установлена (есть бумага), то ...
        printer.println("Paper OK");                  //  Выводим текст
    }else{                                            //  Если кассовая лента отсутствует (нет бумаги), то ...
                                                      //  Тут можно, например, включить внешний светодиод или звуковой сигнал сигнализирующий об окончании кассовой ленты
    }                                                 //
                                                      //
    printer.setDefault();                             //  Устанавливаем настройки принтера по умолчанию. Функцию удобно использовать если Вы желаете вывести текст, но хотите быть уверены что на него не повлияют ранее установленные размеры или начертания.
    printer.feed(3);                                  //  Прокручиваем ленту на 3 строки
}                                                     //
                                                      //
void loop(){}                                         //
